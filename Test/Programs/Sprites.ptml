
TITLE "Sprites"

CONST Player.MoveDelay 30
CONST Player.MissileMoveDelay 20

VAR Player.X 170
VAR Player.Y 95
VAR Player.PrevX Player.X
VAR Player.PrevY Player.Y
VAR Player.Dir 0
VAR Player.MoveCtr 0
VAR Player.MissileX -100
VAR Player.MissileY -100
VAR Player.PrevMissileX 0
VAR Player.PrevMissileY 0
VAR Player.MissileDir 0
VAR Player.MissileDist 0
VAR Player.MissileMoveCtr 0
VAR Player.CanShoot 1
VAR Player.Shooting 0
VAR Key
VAR Screen.BackColor 0x80

Init:
	VOL 1000
	CLS Screen.BackColor
	
MainLoop:
	DRAW
	
	CHK Key SYS.KEY_ESC
	CMP Key 1
	CALL= Game.Reset
	
	CMP Player.Shooting 1
	CALL= Player.DrawMissile
	CALL Player.Draw
	CMP Player.MissileMoveCtr 0
	CALL= Player.TryMissileMove
	INC Player.MissileMoveCtr
	CMP Player.MissileMoveCtr Player.MissileMoveDelay
	CALL= Player.ResetMissileMoveCtr
	
	CHK Key SYS.KEY_SPACE
	CMP Key 1
	CALL= Player.TryShoot

	INC Player.MoveCtr
	CMP Player.MoveCtr Player.MoveDelay
	CALL>= Player.ResetMoveCtr
	CMP Player.MoveCtr 0
	JP!= MainLoop

	CHK Key SYS.KEY_RIGHT
	CMP Key 1
	CALL= Player.MoveRight
	
	CHK Key SYS.KEY_LEFT
	CMP Key 1
	CALL= Player.MoveLeft
	
	CHK Key SYS.KEY_UP
	CMP Key 1
	CALL= Player.MoveUp
	
	CHK Key SYS.KEY_DOWN
	CMP Key 1
	CALL= Player.MoveDown
	
	JP MainLoop
	
Game.Reset:
	RESET
	
Player.ResetMoveCtr:
	SET Player.MoveCtr 0
	RET
Player.TryShoot:
	CMP Player.CanShoot 1
	CALL= Player.Shoot
	RET
Player.Shoot:
	PLAY "C6 D6 E6"
	SET Player.CanShoot 0
	SET Player.MissileX Player.X
	SET Player.MissileY Player.Y
	SET Player.MissileDir Player.Dir
	SET Player.Shooting 1
	RET
Player.TryMissileMove:
	CMP Player.Shooting 1
	CALL= Player.MissileMove
	RET
Player.MissileMove:
	CMP Player.MissileDir 0
	CALL= Player.MissileMoveUp
	CMP Player.MissileDir 1
	CALL= Player.MissileMoveRight
	CMP Player.MissileDir 2
	CALL= Player.MissileMoveDown
	CMP Player.MissileDir 3
	CALL= Player.MissileMoveLeft
	INC Player.MissileDist
	CMP Player.MissileDist 50
	CALL> Player.ResetMissile
	RET
Player.ResetMissileMoveCtr:
	SET Player.MissileMoveCtr 0
	RET
Player.MissileMoveRight:
	SET Player.PrevMissileX Player.MissileX
	SET Player.PrevMissileY Player.MissileY
	INC Player.MissileX
	RET
Player.ResetMissile:
	SET Player.CanShoot 1
	SET Player.Shooting 0
	SET Player.MissileDist 0
	CALL Player.EraseMissilePrevPos
	RET
Player.MissileMoveLeft:
	SET Player.PrevMissileX Player.MissileX
	SET Player.PrevMissileY Player.MissileY
	DEC Player.MissileX
	RET
Player.MissileMoveUp:
	SET Player.PrevMissileX Player.MissileX
	SET Player.PrevMissileY Player.MissileY
	DEC Player.MissileY
	RET
Player.MissileMoveDown:
	SET Player.PrevMissileX Player.MissileX
	SET Player.PrevMissileY Player.MissileY
	INC Player.MissileY
	RET
Player.Draw:
	OUT 2 Player.X Player.Y 0xfe 0 0
	RET
Player.ErasePrevPos:
	OUT 0 Player.PrevX Player.PrevY 0 Screen.BackColor 1
	RET
Player.DrawMissile:
	CALL Player.EraseMissilePrevPos
	OUT 3 Player.MissileX Player.MissileY 0x47 0 0
	RET
Player.EraseMissilePrevPos:
	OUT 0 Player.PrevMissileX Player.PrevMissileY 0 Screen.BackColor 1
	RET
Player.MoveRight:
	SET Player.Dir 1
	CALL Player.SaveXY
	INC Player.X
	CALL Player.ErasePrevPos
	RET
Player.MoveLeft:
	SET Player.Dir 3
	CALL Player.SaveXY
	DEC Player.X
	CALL Player.ErasePrevPos
	RET
Player.MoveUp:
	SET Player.Dir 0
	CALL Player.SaveXY
	DEC Player.Y
	CALL Player.ErasePrevPos
	RET
Player.MoveDown:
	SET Player.Dir 2
	CALL Player.SaveXY
	INC Player.Y
	CALL Player.ErasePrevPos
	RET
Player.SaveXY:
	SET Player.PrevX Player.X
	SET Player.PrevY Player.Y
	RET
